(* RDB file parser *)

let opcode_eof = 0xFF
let opcode_selectdb = 0xFE
let opcode_expiretime = 0xFD
let opcode_expiretimems = 0xFC
let opcode_resizedb = 0xFB
let opcode_aux = 0xFA
let type_string = 0

let read_byte data pos =
  if pos >= String.length data then None
  else Some (Char.code data.[pos], pos + 1)

let read_length data pos =
  match read_byte data pos with
  | None -> None
  | Some (byte, pos') ->
      let typ = (byte land 0xC0) lsr 6 in
      match typ with
      | 0 -> Some (byte land 0x3F, pos')
      | 1 ->
          (match read_byte data pos' with
           | Some (next_byte, pos'') ->
               let len = ((byte land 0x3F) lsl 8) lor next_byte in
               Some (len, pos'')
           | None -> None)
      | 2 ->
          if pos' + 3 < String.length data then
            let len =
              (Char.code data.[pos']) lor
              (Char.code data.[pos' + 1] lsl 8) lor
              (Char.code data.[pos' + 2] lsl 16) lor
              (Char.code data.[pos' + 3] lsl 24)
            in
            Some (len, pos' + 4)
          else None
      | _ -> None

let read_string data pos =
  match read_length data pos with
  | None -> None
  | Some (len, pos') ->
      if pos' + len <= String.length data then
        let s = String.sub data pos' len in
        Some (s, pos' + len)
      else None

let rec parse_pairs data pos acc =
  match read_byte data pos with
  | None -> List.rev acc
  | Some (byte, pos') ->
      if byte = opcode_eof then
        List.rev acc
      else if byte = opcode_selectdb then
        (match read_length data pos' with
         | Some (_, pos'') -> parse_pairs data pos'' acc
         | None -> List.rev acc)
      else if byte = opcode_aux then
        (match read_string data pos' with
         | Some (_, pos'') ->
             (match read_string data pos'' with
              | Some (_, pos''') -> parse_pairs data pos''' acc
              | None -> List.rev acc)
         | None -> List.rev acc)
      else if byte = opcode_resizedb then
        (match read_length data pos' with
         | Some (_, pos'') ->
             (match read_length data pos'' with
              | Some (_, pos''') -> parse_pairs data pos''' acc
              | None -> List.rev acc)
         | None -> List.rev acc)
      else if byte = opcode_expiretime || byte = opcode_expiretimems then
        let skip_bytes = if byte = opcode_expiretime then 4 else 8 in
        let pos_after_expiry = pos' + skip_bytes in
        (match read_byte data pos_after_expiry with
         | Some (value_type, pos_vt) ->
             if value_type = type_string then
               (match read_string data pos_vt with
                | Some (key, pos_k) ->
                    (match read_string data pos_k with
                     | Some (value, pos_v) ->
                         parse_pairs data pos_v ((key, Resp.BulkString value) :: acc)
                     | None -> List.rev acc)
                | None -> List.rev acc)
             else
               List.rev acc
         | None -> List.rev acc)
      else if byte = type_string then
        (match read_string data pos' with
         | Some (key, pos_k) ->
             (match read_string data pos_k with
              | Some (value, pos_v) ->
                  parse_pairs data pos_v ((key, Resp.BulkString value) :: acc)
              | None -> List.rev acc)
         | None -> List.rev acc)
      else
        List.rev acc

let parse_file filename =
  try
    let ic = open_in_bin filename in
    let len = in_channel_length ic in
    let data = really_input_string ic len in
    close_in ic;

    if String.length data < 9 || String.sub data 0 5 <> "REDIS" then
      []
    else
      parse_pairs data 9 []
  with
  | _ -> []
